/****************************************************************************
Copyright (C) 2017 by c3r1c3 <c3r1c3@nevermindonline.com>

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 2 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*****************************************************************************/

/*
 * Based on
 * http://www.geeks3d.com/20091009/shader-library-night-vision-post-processing-filter-glsl/
 */

/* Standard OBS->GPU uploads. */
uniform float4x4 ViewProj;
uniform texture2d image;

/* Filter variables from OBS. */
uniform texture2d noise_texture;
uniform float sin_movement;
uniform float cos_movement;
uniform float luminance_threshold;
uniform float luminance_boost;
uniform float color_amplification;
uniform float3 hvc;
uniform float3 vision_color;


/*
 * Here we set how the base texture will be sampled/read/interpreted.
 * Below is what you would normally use in OBS. Why?
 * Linear filtering 'messes' with the image the least.
 */
sampler_state textureSampler {
	Filter    = Linear;
	AddressU  = Clamp;
	AddressV  = Clamp;
};

/* Here we set how the noise texture will be sampled/read/interpreted. */
sampler_state noiseSampler {
	Filter    = Anisotropic;
	AddressU  = Repeat;
	AddressV  = Repeat;
};

/*
 * Here we create the struct to hold the current position and texture info
 * of the vertex in question.
 */
struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

/*
 * Now we transform the vertices to world space (and the
 * textures to go with it). In OBS you'll usually want to
 * use the code below, unless you're trying to create a
 * '3D'-esqe effect.
 */
VertData VSDefault(VertData vert_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(vert_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv = vert_in.uv;
	return vert_out;
}

/*
 * Lastly we have the function that transforms the texture into
 * the "Night Vision" version of itself.
 */
float4 PSNightVision(VertData vert_in) : TARGET
{
	float4 imageSample = image.Sample(textureSampler, vert_in.uv);
	float2 noiseSampleUV;

	noiseSampleUV.x = sin_movement;
	noiseSampleUV.y = cos_movement;

	float3 noiseSample = noise_texture.Sample(noiseSampler, (vert_in.uv *
		float2(3.5, 3.5)) + noiseSampleUV).rgb;
	float3 colorUp = image.Sample(textureSampler, vert_in.uv +
		(noiseSample.xy * float2(0.005,0.005))).rgb;

	float luminance = dot(hvc, colorUp);
	if (luminance < luminance_threshold)
	{
		colorUp *= color_amplification;
		imageSample.rgb = (colorUp + (noiseSample *
				luminance_boost)) * vision_color;
	}
	else
	{
		imageSample.rb = float2(0.0, 0.0);
	}

	return imageSample;
}

/* This call actually executes the above functions on the GPU. */
technique Draw
{
	pass
	{
		vertex_shader = VSDefault(vert_in);
		pixel_shader  = PSNightVision(vert_in);
	}
}